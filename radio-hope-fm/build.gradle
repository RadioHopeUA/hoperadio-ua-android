def configuration = rootProject.ext.configuration

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion configuration.compileSdkVersion
    buildToolsVersion configuration.buildToolsVersion

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "ua.hope.radio.hopefm"
        minSdkVersion configuration.minSdkVersion
        targetSdkVersion configuration.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            File signFile = project.file("signing/HopeFM.properties")
            if (signFile.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(signFile))

                storeFile project.file("signing/" + props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [ applicationLabel: "@string/app_name_dev" ]
            applicationIdSuffix ".debug"
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }
        release {
            manifestPlaceholders = [ applicationLabel: "@string/app_name"]
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../radio-core/proguard-rules.pro', 'proguard-rules.pro'
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${variant.name}-${variant.versionName}.${variant.versionCode}.apk"
        }
    }
}

dependencies {
    implementation project(path: ':radio-core')
}
